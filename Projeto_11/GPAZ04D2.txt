      *--------------------------------------------------------------*
      *                                                              *
      *   OBJETIVO:                                                  *
      *                                                              *
      *                                                              *
      *--------------------------------------------------------------*
      *--------------------------------------------------------------*
       IDENTIFICATION                      DIVISION.
      *--------------------------------------------------------------*
       PROGRAM-ID.                         GPAZ04D2.
       AUTHOR.                             FABIANO.
       DATE-WRITTEN.                       07/10/2020.
       DATE-COMPILED.                      07/10/2020.
       SECURITY.                           NENHUMA.
      *--------------------------------------------------------------*
       ENVIRONMENT                         DIVISION.
      *--------------------------------------------------------------*
       CONFIGURATION                       SECTION.
       SOURCE-COMPUTER.                    NOTEFABIANO.
       OBJECT-COMPUTER.                    HERCULES1.
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.
      *
       INPUT-OUTPUT                        SECTION.
      * ETAPA 1: ESPECIFICACAO DOS ARQUIVOS DE ENTRADA E SAIDA
       FILE-CONTROL.
           SELECT MOVPRDD2 ASSIGN             TO UT-S-MOVPRDD2
           FILE STATUS IS FS-MOVPRDD2
           .
      *--------------------------------------------------------------*
       DATA                                DIVISION.
      *--------------------------------------------------------------*
       FILE                                SECTION.
      * ETAPA 2: DETALHAMANETO DOS ARQUIVOS DE ENTRADA E SAIDA
      *
       FD  MOVPRDD2
           RECORDING MODE IS F
           .
           COPY BK002D2 REPLACING ==: :== BY ====.
      *--------------------------------------------------------------*
       WORKING-STORAGE                     SECTION.
      *--------------------------------------------------------------*
      * BOOK DA VARIAVEL ESPELHO
      *
           COPY BK002D2 REPLACING ==: :== BY ==WS-==.
      *
      * BOOK DAS VARIAVEIS PARA CALCULO DO TEMPO
           COPY VARTEMPO.
           COPY VARDATA.
      *
      * BOOK DAS VARIAVEIS HOSPEDEIRAS
      *
           EXEC SQL
              INCLUDE BK002TP
           END-EXEC
      * BOOK DAS VARIAVEIS PARA USO DO SQL
      *
           EXEC SQL
              INCLUDE SQLCA
           END-EXEC
      *
      * INDICATORS
      *
       01  INDICADORES.
           05 ICODPROD                     PIC S9(4) COMP.
           05 IDESCPROD                    PIC S9(4) COMP.
           05 IUNIDPROD                    PIC S9(4) COMP.
           05 ILOCALPROD                   PIC S9(4) COMP.
           05 IQTDEST                      PIC S9(4) COMP.
           05 IQTDMAX                      PIC S9(4) COMP.
           05 IQTDMIN                      PIC S9(4) COMP.
           05 IPRECOCOMPRA                 PIC S9(4) COMP.
           05 IPRECOVENDA                  PIC S9(4) COMP.
           05 IPERCOMIS                    PIC S9(4) COMP.
           05 ITIPOMOVTO                   PIC S9(4) COMP.
      *
      * DECLARANDO AS VARIAVEIS CONTADORAS
       01  WS-CONTADORES-COMP.
           05 WS-CTLIDO                    PIC 9(05) COMP.
           05 WS-CTINS                     PIC 9(05) COMP.
           05 WS-CTEXC                     PIC 9(05) COMP.
           05 WS-CTINV                     PIC 9(05) COMP.
           05 WS-CTALT                     PIC 9(05) COMP.
           05 WS-CTCON                     PIC 9(05) COMP.
      *
      * DECLARANDO AS VARIAVEIS FORMATADAS
       01  WS-CONTADORES-FORMATADOS.
           05 WS-CTLIDO-F                  PIC ZZ.ZZ9.
           05 WS-CTINS-F                   PIC ZZ.ZZ9.
           05 WS-CTEXC-F                   PIC ZZ.ZZ9.
           05 WS-CTINV-F                   PIC ZZ.ZZ9.
           05 WS-CTALT-F                   PIC ZZ.ZZ9.
           05 WS-CTCON-F                   PIC ZZ.ZZ9.
      *
      * DECLARANDO AS VARIAVEIS DE FILE STATUS
       01  FS-MOVPRDD2                     PIC X(02).
           88  SUCESSO-MOV                          VALUE "00".
           88  FIM-ARQ-MOV                          VALUE "10".
      *
       01  VAR-CONSULTA.
           05 CODPROD-FF                   PIC 9999.
           05 QTDEST-FF                    PIC ZZ.ZZ9.
           05 QTDMAX-FF                    PIC ZZ.ZZ9.
           05 QTDMIN-FF                    PIC ZZ.ZZ9.
           05 PRECOCOMPRA-FF               PIC ZZZ.ZZ9,99.
           05 PRECOVENDA-FF                PIC ZZZ.ZZ9,99.
           05 PERCOMIS-FF                  PIC Z9,99.
      *
       77  WS-SQLCODE                      PIC +9(9).
       77  WS-MSG                          PIC X(60).
       77  WS-FS                           PIC X(02).
      *--------------------------------------------------------------*
      * MENSAGENS DE ERRO DE FILE STATUS
      *--------------------------------------------------------------*
       77  WS-MSG01                        PIC X(60)
                                                   VALUE
           "ERRO ABERTURA MOVPRDD2".
       77  WS-MSG02                        PIC X(60)
                                                   VALUE
           "ERRO LEITURA MOVPRDD2".
       77  WS-MSG03                        PIC X(60)
                                                   VALUE
           "ERRO INSERT NA TABELA TBPRODUTO".
       77  WS-MSG04                        PIC X(60)
                                                   VALUE
           "ERRO DELETE NA TABELA TBPRODUTO".
       77  WS-MSG05                        PIC X(60)
                                                   VALUE
           "ERRO SELECT NA TABELA TBPRODUTO".
       77  WS-MSG06                        PIC X(60)
                                                   VALUE
           "ERRO UPDATE NA TABELA TBPRODUTO".
       77  WS-MSG07                        PIC X(60)
                                                   VALUE
           "ERRO CONSULTA NA TABELA TBPRODUTO".
       77  WS-MSG08                        PIC X(60)
                                                   VALUE
           "ERRO FECHAMENTO MOVPRDD2".
      *--------------------------------------------------------------*
       LINKAGE                             SECTION.
      * ESTA SECTION FICARA VAZIA POIS NAO RECEBE DADOS DE OUTROS PROG
      *--------------------------------------------------------------*
       PROCEDURE                           DIVISION.
      *--------------------------------------------------------------*
      * ETAPA 3: MANIPULACAO DOS ARQUIVOS DE ENTRADA E SAIDA
      *
           PERFORM 1000-INICIALIZAR
           PERFORM 3000-PROCESSAR UNTIL FIM-ARQ-MOV
           PERFORM 4000-TERMINO
           STOP RUN
           .
       1000-INICIALIZAR.
           ACCEPT WS-HORARIO-INICIAL FROM TIME
           MOVE 0                          TO WS-CTLIDO
                                              WS-CTINS
                                              WS-CTEXC
                                              WS-CTINV
                                              WS-CTALT
                                              WS-CTCON
      *
           OPEN INPUT MOVPRDD2
           IF NOT SUCESSO-MOV
              MOVE WS-MSG01                TO WS-MSG
              MOVE FS-MOVPRDD2             TO WS-FS
              GO TO 9999-ERRO
           END-IF
      *
           PERFORM 2000-LER-MOVPRDD2
           .
       2000-LER-MOVPRDD2.
           READ MOVPRDD2 INTO WS-REG-MOVPRDD2
           IF SUCESSO-MOV
              ADD 1                        TO WS-CTLIDO
           ELSE
              IF NOT FIM-ARQ-MOV
                 MOVE WS-MSG02             TO WS-MSG
                 MOVE FS-MOVPRDD2          TO WS-FS
                 GO TO 9999-ERRO
              END-IF
           END-IF
           .
       3000-PROCESSAR.
           EVALUATE WS-TIPOMOVTO-D2
              WHEN "I" PERFORM 3100-INCLUSAO
              WHEN "E" PERFORM 3200-EXCLUSAO
              WHEN "A" PERFORM 3300-ALTERACAO
              WHEN "C" PERFORM 3400-CONSULTAR
              WHEN OTHER
                 ADD 1                     TO WS-CTINV
           END-EVALUATE.
      *
           PERFORM 2000-LER-MOVPRDD2
           .
       3100-INCLUSAO.
      *----------------------------------------------------------------*
      * MOVER DADOS DA VARIAVEL ESPELHO PARA A HOSPEDEIRA
      *----------------------------------------------------------------*
           MOVE WS-CODPROD-D2              TO CODPROD
           MOVE +20                        TO DESCPROD-LEN
           MOVE WS-DESCPROD-D2             TO DESCPROD-TEXT
           MOVE WS-UNIDPROD-D2             TO UNIDPROD
           MOVE WS-LOCALPROD-D2            TO LOCALPROD
           MOVE WS-QTDEST-D2               TO QTDEST
           MOVE WS-QTDMAX-D2               TO QTDMAX
           MOVE WS-QTDMIN-D2               TO QTDMIN
           MOVE WS-PRECOCOMPRA-D2          TO PRECOCOMPRA
           MOVE WS-PRECOVENDA-D2           TO PRECOVENDA
           MOVE WS-PERCOMIS-D2             TO PERCOMIS
      *----------------------------------------------------------------*
      * INSERIR OS DADOS DA HOSPEDEIRA NA TABELA (SQL FIELDS)
      *----------------------------------------------------------------*
           EXEC SQL
              INSERT INTO TBPRODUTO
              (   CODPROD
                , DESCPROD
                , UNIDPROD
                , LOCALPROD
                , QTDEST
                , QTDMAX
                , QTDMIN
                , PRECOCOMPRA
                , PRECOVENDA
                , PERCOMIS
              )
              VALUES
              (   :CODPROD
                , :DESCPROD
                , :UNIDPROD
                , :LOCALPROD
                , :QTDEST
                , :QTDMAX
                , :QTDMIN
                , :PRECOCOMPRA
                , :PRECOVENDA
                , :PERCOMIS
              )
           END-EXEC
      *----------------------------------------------------------------*
      * TESTAR SQLCODE
      *----------------------------------------------------------------*
           EVALUATE SQLCODE
              WHEN 0
                 ADD 1                     TO WS-CTINS
              WHEN -803
                 ADD 1                     TO WS-CTINV
              WHEN -545
                 ADD 1                     TO WS-CTINV
              WHEN OTHER
                 MOVE WS-MSG03             TO WS-MSG
                 MOVE SQLCODE              TO WS-SQLCODE
                 GO TO 9000-ERRO-DB2
           END-EVALUATE
           .
       3200-EXCLUSAO.
      * MOVER CODIGO DO PRODUTO DA VARIAVEL ESPELHO PARA A HOSPEDEIRA
           MOVE WS-CODPROD-D2              TO CODPROD
      * EXCLUIR O PRODUTO DA TABELA USANDO DELETE
           EXEC SQL
              DELETE FROM TBPRODUTO
              WHERE CODPROD = :CODPROD
           END-EXEC
      *
           EVALUATE SQLCODE
              WHEN 0 ADD 1                 TO WS-CTEXC
              WHEN +100 ADD 1              TO WS-CTINV
              WHEN OTHER
                 MOVE WS-MSG04             TO WS-MSG
                 MOVE SQLCODE              TO WS-SQLCODE
                 GO TO 9000-ERRO-DB2
           END-EVALUATE
           .
       3300-ALTERACAO.
           MOVE WS-CODPROD-D2              TO CODPROD
      *
           EXEC SQL
              SELECT CODPROD
                   , DESCPROD
                   , UNIDPROD
                   , LOCALPROD
                   , QTDEST
                   , QTDMAX
                   , QTDMIN
                   , PRECOCOMPRA
                   , PRECOVENDA
                   , PERCOMIS
              INTO  :CODPROD
                   ,:DESCPROD       :IDESCPROD
                   ,:UNIDPROD       :IUNIDPROD
                   ,:LOCALPROD      :ILOCALPROD
                   ,:QTDEST         :IQTDEST
                   ,:QTDMAX         :IQTDMAX
                   ,:QTDMIN         :IQTDMIN
                   ,:PRECOCOMPRA    :IPRECOCOMPRA
                   ,:PRECOVENDA     :IPRECOVENDA
                   ,:PERCOMIS       :IPERCOMIS
              FROM TBPRODUTO
              WHERE CODPROD = :CODPROD
           END-EXEC
      *
           EVALUATE SQLCODE
              WHEN 0
                 PERFORM 9000-INDICATOR
                 PERFORM 3310-UPDATE
              WHEN +100
                 ADD 1                     TO WS-CTINV
              WHEN OTHER
                 MOVE WS-MSG05             TO WS-MSG
                 MOVE SQLCODE              TO WS-SQLCODE
                 GO TO 9000-ERRO-DB2
           END-EVALUATE
           .
       3310-UPDATE.
           MOVE WS-CODPROD-D2              TO CODPROD
      *
           IF WS-DESCPROD-D2 NOT = SPACES
              MOVE +20                     TO DESCPROD-LEN
              MOVE WS-DESCPROD-D2          TO DESCPROD-TEXT
           END-IF
      *
           IF WS-UNIDPROD-D2 NOT = SPACES
              MOVE WS-UNIDPROD-D2          TO UNIDPROD
           END-IF
      *
           IF WS-LOCALPROD-D2 NOT = SPACES
              MOVE WS-LOCALPROD-D2         TO LOCALPROD-D2
           END-IF
      *
           IF WS-QTDEST-D2 IS NUMERIC
              MOVE WS-QTDEST-D2            TO QTDEST-D2
           END-IF
      *
           IF WS-QTDMAX-D2 IS NUMERIC
              MOVE WS-QTDMAX-D2             TO QTDMAX-D2
           END-IF
      *
           IF WS-QTDMIN-D2 IS NUMERIC
              MOVE WS-QTDMIN-D2             TO QTDMIN-D2
           END-IF
      *
           IF WS-PRECOCOMPRA-D2 IS NUMERIC
              MOVE WS-PRECOCOMPRA-D2       TO PRECOCOMPRA-D2
           END-IF
      *
           IF WS-PRECOVENDA-D2 IS NUMERIC
              MOVE WS-PRECOVENDA-D2       TO PRECOVENDA-D2
           END-IF
      *
           IF WS-PERCOMIS-D2 IS NUMERIC
              MOVE WS-PERCOMIS-D2       TO PERCOMIS-D2
           END-IF
      *
           EXEC SQL
              UPDATE TBPRODUTO
              SET DESCPROD    = :DESCPROD
                 ,UNIDPROD    = :UNIDPROD
                 ,LOCALPROD   = :LOCALPROD
                 ,QTDEST      = :QTDEST
                 ,QTDMAX      = :QTDMAX
                 ,QTDMIN      = :QTDMIN
                 ,PRECOCOMPRA = :PRECOCOMPRA
                 ,PRECOVENDA  = :PRECOVENDA
                 ,PERCOMIS    = :PERCOMIS
              WHERE CODPROD   = :CODPROD
           END-EXEC
      *
           EVALUATE SQLCODE
              WHEN 0    ADD 1              TO WS-CTALT
              WHEN +100 ADD 1              TO WS-CTINV
              WHEN -545 ADD 1              TO WS-CTINV
              WHEN OTHER
                   MOVE WS-MSG06           TO WS-MSG
                   MOVE SQLCODE            TO WS-SQLCODE
                   GO TO 9000-ERRO-DB2
           END-EVALUATE
           .
       3400-CONSULTAR.
           MOVE WS-CODPROD-D2              TO CODPROD
      *
           EXEC SQL
              SELECT CODPROD
                   , DESCPROD
                   , UNIDPROD
                   , LOCALPROD
                   , QTDEST
                   , QTDMAX
                   , QTDMIN
                   , PRECOCOMPRA
                   , PRECOVENDA
                   , PERCOMIS
              INTO  :CODPROD
                   ,:DESCPROD       :IDESCPROD
                   ,:UNIDPROD       :IUNIDPROD
                   ,:LOCALPROD      :ILOCALPROD
                   ,:QTDEST         :IQTDEST
                   ,:QTDMAX         :IQTDMAX
                   ,:QTDMIN         :IQTDMIN
                   ,:PRECOCOMPRA    :IPRECOCOMPRA
                   ,:PRECOVENDA     :IPRECOVENDA
                   ,:PERCOMIS       :IPERCOMIS
              FROM TBPRODUTO
              WHERE CODPROD = :CODPROD
           END-EXEC
      *
           EVALUATE SQLCODE
              WHEN 0 ADD 1                 TO WS-CTCON
                     PERFORM 9000-INDICATOR
                     PERFORM 9000-IMPRIME-PRODUTO
              WHEN +100 ADD 1              TO WS-CTINV
              WHEN OTHER
              MOVE WS-MSG07                TO WS-MSG
              MOVE SQLCODE                 TO WS-SQLCODE
              GO TO 9000-ERRO-DB2
           END-EVALUATE
           .
       4000-TERMINO.
      *
           CLOSE MOVPRDD2
           IF NOT SUCESSO-MOV
               MOVE WS-MSG08               TO WS-MSG
               MOVE FS-MOVPRDD2            TO WS-FS
               GO TO 9999-ERRO
           END-IF
      *
           ACCEPT WS-HORARIO-FINAL FROM TIME
      *
           COPY CALCTEMP.
      *
           MOVE WS-CTLIDO                  TO WS-CTLIDO-F
           MOVE WS-CTINS                   TO WS-CTINS-F
           MOVE WS-CTEXC                   TO WS-CTEXC-F
           MOVE WS-CTCON                   TO WS-CTCON-F
           MOVE WS-CTALT                   TO WS-CTALT-F
           MOVE WS-CTINV                   TO WS-CTINV-F
      *
           PERFORM 9000-IMPRIME-DATA
      *
           DISPLAY "   "
           DISPLAY "================================================="
           DISPLAY "==    ESTATISTICA FINAL DE PROCESSAMENTO       =="
           DISPLAY "================================================="
           DISPLAY "QTDE. PRODUTOS LIDOS MOVPRDD2........: " WS-CTLIDO-F
           DISPLAY "QTDE. PRODUTOS INSERIDOS TBPRODUTO...: " WS-CTINS-F
           DISPLAY "QTDE. PRODUTOS EXCLUIDOS TBPRODUTO...: " WS-CTEXC-F
           DISPLAY "QTDE. PRODUTOS ALTERADOS TBPRODUTO...: " WS-CTALT-F
           DISPLAY "QTDE. PRODUTOS CONSULTADOS TBPRODUTO.: " WS-CTCON-F
           DISPLAY "QTDE. PRODUTOS INVALIDOS.............: " WS-CTINV-F
           DISPLAY "================================================="
           DISPLAY "TEMPO TOTAL DE PROCESSAMENTO.........: "
                    WS-TEMPO-PROCESSAMENTO
           DISPLAY "================================================="
           DISPLAY "==     TERMINO NORMAL DO PROGRAMA GPAZ04D2     =="
           DISPLAY "================================================="
           .
      *
       9000-ERRO-DB2.
           DISPLAY "================================================="
           DISPLAY "MESSAGE..: " WS-MSG
           DISPLAY "SQLCODE..: " WS-SQLCODE
           DISPLAY "SQLERRMC.: " SQLERRMC
           DISPLAY "================================================="
           DISPLAY "==    TERMINO ANORMAL DO PROGRAMA GPAZ04D2     =="
           DISPLAY "================================================="
           MOVE 16                         TO RETURN-CODE
           STOP RUN
           .
       9000-INDICATOR.
      * TESTE DOS INDICATORS
           IF IDESCPROD LESS ZERO
              MOVE SPACES                   TO DESCPROD
           END-IF
      *
           IF IUNIDPROD LESS ZERO
              MOVE SPACES                   TO UNIDPROD
           END-IF
      *
           IF ILOCALPROD LESS ZERO
              MOVE SPACES                   TO LOCALPROD
           END-IF
      *
           IF IQTDEST LESS ZERO
              MOVE ZEROS                    TO QTDEST
           END-IF
      *
           IF IQTDMAX LESS ZERO
              MOVE ZEROS                    TO QTDMAX
           END-IF
      *
           IF IQTDMIN LESS ZERO
              MOVE ZEROS                    TO QTDMIN
           END-IF
      *
           IF IPRECOCOMPRA LESS ZERO
              MOVE ZEROS                    TO PRECOCOMPRA
           END-IF
      *
           IF IPRECOVENDA LESS ZERO
              MOVE ZEROS                    TO PRECOVENDA
           END-IF
      *
           IF IPERCOMIS LESS ZERO
              MOVE ZEROS                    TO PERCOMIS
           END-IF
           .
       9000-IMPRIME-PRODUTO.
           MOVE CODPROD                    TO CODPROD-FF
           MOVE QTDEST                     TO QTDEST-FF
           MOVE QTDMAX                     TO QTDMAX-FF
           MOVE QTDMIN                     TO QTDMIN-FF
           MOVE PRECOCOMPRA                TO PRECOCOMPRA-FF
           MOVE PRECOVENDA                 TO PRECOVENDA-FF
           MOVE PERCOMIS                   TO PERCOMIS-FF
           DISPLAY "=================================================="
           DISPLAY "==            CONSULTA DE PRODUTOS              =="
           DISPLAY "=================================================="
           DISPLAY "CODIGO...................: "  CODPROD
           DISPLAY "DESCRICAO................: "  DESCPROD-TEXT
           DISPLAY "UNIDADE DE VENDA.........: "  UNIDPROD
           DISPLAY "LOCALIZACAO NO ESTOQUE...: "  LOCALPROD
           DISPLAY "QTDE. EM ESTOQUE.........: "  QTDEST
           DISPLAY "QTDE. MAXIMA ARMAZENADA..: "  QTDMAX
           DISPLAY "QTDE. MINIMA ARMAZENADA..: "  QTDMIN
           DISPLAY "PRECO DE COMPRA..........: "  PRECOCOMPRA
           DISPLAY "PRECO DE VENDA...........: "  PRECOVENDA
           DISPLAY "PERCENTUAL DE COMISSAO...: "  PERCOMIS
           DISPLAY "=================================================="
           .
           COPY ROTDATA.
           COPY ROTERRO.

