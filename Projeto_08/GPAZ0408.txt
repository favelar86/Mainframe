      *--------------------------------------------------------------*
      *                                                              *
      *   OBJETIVO: ATUALIZAR CADASTRO DE PRODUTOS E GERAR           *
      *             RELATORIO COM AS MUDANCAS EFETUADAS              *
      *                                                              *
      *--------------------------------------------------------------*
      *--------------------------------------------------------------*
       IDENTIFICATION                      DIVISION.
      *--------------------------------------------------------------*
       PROGRAM-ID.                         GPAZ0408.
       AUTHOR.                             FABIANO.
       DATE-WRITTEN.                       23/09/2020.
       DATE-COMPILED.                      24/09/2020.
       SECURITY.                           NENHUMA.
      *--------------------------------------------------------------*
       ENVIRONMENT                         DIVISION.
      *--------------------------------------------------------------*
       CONFIGURATION                       SECTION.
       SOURCE-COMPUTER.                    NOTEFABIANO.
       OBJECT-COMPUTER.                    HERCULES1.
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.
      *
       INPUT-OUTPUT                        SECTION.
      * ETAPA 1: ESPECIFICACAO DOS ARQUIVOS DE ENTRADA E SAIDA
       FILE-CONTROL.
           SELECT MOVPRD ASSIGN            TO DA-I-MOVPRD
           FILE STATUS IS FS-MOVPRD
           .
           SELECT VCADPRD ASSIGN           TO DA-I-VCADPRD
           ORGANIZATION IS INDEXED
           ACCESS MODE IS RANDOM
           FILE STATUS IS FS-VCADPRD
           RECORD KEY IS CODPROD-V
           .
           SELECT RELOCOR ASSIGN           TO UT-S-RELOCOR
           FILE STATUS IS FS-RELOCOR
           .
      *--------------------------------------------------------------*
       DATA                                DIVISION.
      *--------------------------------------------------------------*
       FILE                                SECTION.
      * ETAPA 2: DETALHAMANETO DOS ARQUIVOS DE ENTRADA E SAIDA
      *
       FD  MOVPRD
           LABEL RECORD STANDARD
           BLOCK CONTAINS 0 RECORDS
           RECORDING MODE IS F
           RECORD CONTAINS 70 CHARACTERS
           DATA RECORD IS REG-MOVPRD
           .
           COPY MOVPRD REPLACING ==:XX-:== BY ====.
      *
       FD  VCADPRD
           RECORD CONTAINS 69 CHARACTERS
           DATA RECORD IS REG-VCADPRD
           .
           COPY VCADPRD REPLACING ==:XX-:== BY ====.
      *
       FD  RELOCOR
           LABEL RECORD OMITTED
           RECORDING MODE IS F
           RECORD CONTAINS 132 CHARACTERS
           DATA RECORD IS REG-RELOCOR
           .
       01  REG-RELOCOR                    PIC X(132).
      *
       WORKING-STORAGE                     SECTION.
      *--------------------------------------------------------------*
      * PRIMEIRA LINHA
       01  WS-CABEC1.
           05 WS-DATA-CABEC1               PIC X(10).
           05 FILLER                       PIC X(02)
                                                   VALUE SPACES.
           05 WS-HORA-CABEC1               PIC X(08)
                                                   VALUE SPACES.
           05 FILLER                       PIC X(30)
                                                   VALUE SPACES.
           05 FILLER                       PIC X(44)
                                                   VALUE
             "OCORRENCIAS ATUALIZACAO CADASTRO DE PRODUTOS".
           05 FILLER                       PIC X(31)
                                                   VALUE SPACES.
           05 FILLER                       PIC X(05)
                                                   VALUE
             "PAG. ".
           05 WS-PAG-CABEC1                PIC Z9.
      * SEGUNDA LINHA
       01 WS-CABEC2.
          05 FILLER                        PIC X(132)
                                                   VALUE ALL "-".
      * TRECEIRA LINHA
       01 WS-CABEC3.
          05 FILLER                        PIC X(05)
                                                   VALUE SPACES.
          05 FILLER                        PIC X(18)
                                                   VALUE
           "IMAGEM DO REGISTRO".
          05 FILLER                        PIC X(82)
                                                   VALUE SPACES.
          05 FILLER                        PIC X(10)
                                                   VALUE
           "OCORRENCIA".
          05 FILLER                        PIC X(17)
                                                   VALUE SPACES.
      * QUARTA LINHA
       01 WS-CABEC4.
          05 FILLER                        PIC X(132)
                                                   VALUE ALL "-".
      *--------------------------------------------------------------*
      *                      LINHA DE DETALHE                        *
      *--------------------------------------------------------------*
       01  WS-LINDET.
           05 FILLER                       PIC X(05)
                                                   VALUE SPACES.
      * IMAGEM
           05 LD-IMAGEM                    PIC X(70).
           05 FILLER                       PIC X(30)
                                                   VALUE SPACES.
      * OCORRENCIA
           05 LD-OCORRENCIA                PIC X(18).
           05 FILLER                       PIC X(09)
                                                   VALUE SPACES.
      *--------------------------------------------------------------*
      *                           RODAPE                             *
      *--------------------------------------------------------------*
       01  WS-RODAPE1                      PIC X(132)
                                                   VALUE ALL "-".
       01  WS-RODAPE2.
           05 FILLER                       PIC X(42)
                                                   VALUE
           "INDUSTRIAS GUITAR IN A BOX INDUSTRIES S/A.".
           05 FILLER                       PIC X(58)
                                                   VALUE SPACES.
           05 FILLER                       PIC X(32)
                                                   VALUE
           "SERVIMOS BEM PARA SERVIR SEMPRE.".
      *
      * VARIAVEL ESPELHO MOVPRD
      *
           COPY MOVPRD REPLACING ==:XX-:== BY ==WS-==.
      *
      * VARIAVEL ESPELHO VCADPRD
      *
           COPY VCADPRD REPLACING ==:XX-:== BY ==WS-==.
      *
      * VARIAVEIS DE DATA E HORA
      *
       01  WS-DATA-SYS.
           05 WS-ANO                       PIC 99.
           05 WS-MES                       PIC 99.
           05 WS-DIA                       PIC 99.
      *
       01  WS-DATA-SYS-F.
           05 WS-DIA                       PIC 99.
           05 FILLER                       PIC X(01)
                                                   VALUE "/".
           05 WS-MES                       PIC 99.
           05 FILLER                       PIC X(03)
                                                   VALUE "/20".
           05 WS-ANO                       PIC 99.
      *
       01  WS-HORA-SYS.
           05 WS-HORA                      PIC 99.
           05 WS-MINUTO                    PIC 99.
           05 WS-SEGUNDO                   PIC 99.
      *
       01  WS-HORA-SYS-F.
           05 WS-HORA                      PIC 99.
           05 FILLER                       PIC X(01)
                                                   VALUE ":".
           05 WS-MINUTO                    PIC 99.
           05 FILLER                       PIC X(01)
                                                   VALUE ":".
           05 WS-SEGUNDO                   PIC 99.
      *
      * DECLARANDO AS VARIAVEIS CONTADORAS
       77  WS-CTLIDO                       PIC 9(03) COMP.
       77  WS-CTIMP                        PIC 9(03) COMP.
       77  WS-CTLIN                        PIC 9(02) COMP.
       77  WS-CTPAG                        PIC 9(04) COMP.
       77  WS-CTPULA                       PIC 9(02) COMP.
       77  WS-CTINCL                       PIC 9(04) COMP.
       77  WS-CTEXCL                       PIC 9(04) COMP.
       77  WS-CTALT                        PIC 9(04) COMP.
       77  WS-CTINV                        PIC 9(04) COMP.
      *--------------------------------------------------------------*
      * DECLARANDO AS VARIAVEIS FORMATADAS
       77  WS-CTLIDO-F                     PIC ZZZ9.
       77  WS-CTIMP-F                      PIC ZZZ9.
       77  WS-CTLIN-F                      PIC ZZZ9.
       77  WS-CTPAG-F                      PIC ZZZ9.
       77  WS-CTINCL-F                     PIC ZZZ9.
       77  WS-CTEXCL-F                     PIC ZZZ9.
       77  WS-CTALT-F                      PIC ZZZ9.
       77  WS-CTINV-F                      PIC ZZZ9.
      *
      * DECLARANDO AS VARIAVEIS DE FILE STATUS
       01  FS-MOVPRD                       PIC X(02).
           88 FIM-ARQ-MOVPRD                       VALUE "10".
           88 SUCESSO-ARQ-MOVPRD                   VALUE "00".
      *
       01  FS-VCADPRD                      PIC X(02).
           88 FIM-ARQ-VCADPRD                      VALUE "10".
           88 SUCESSO-ARQ-VCADPRD                  VALUE "00".
           88 CHAVE-DUPLICADA-VCADPRD              VALUE "22".
           88 ARQ-NAO-ENCONTRADO-VCADPRD           VALUE "23".
      *
       01  FS-RELOCOR                      PIC X(02).
           88 FIM-ARQ-RELOCOR                      VALUE "10".
           88 SUCESSO-ARQ-RELOCOR                  VALUE "00".
      *
       77  WS-FS                           PIC X(02).
       77  WS-MSG                          PIC X(60).
      *
           COPY VARTEMPO.
           COPY VARDATA.
      *
      *--------------------------------------------------------------*
      * MENSAGENS DE ERRO DE FILE STATUS
      *--------------------------------------------------------------*
       77  WS-MSG01                        PIC X(60)
                                                   VALUE
           "ERRO ABERTURA MOVPRD".
       77  WS-MSG02                       PIC X(60)
                                                   VALUE
           "ERRO ABERTURA RELOCOR".
       77  WS-MSG03                        PIC X(60)
                                                   VALUE
           "ERRO ABERTURA VCADPRD".
       77  WS-MSG04                        PIC X(60)
                                                   VALUE
           "ERRO LEITURA MOVPRD".
       77  WS-MSG05                        PIC X(60)
                                                   VALUE
           "FILE STATUS NAO ESPERADO".
       77  WS-MSG06                        PIC X(60)
                                                   VALUE
           "ERRO DE IMPRESSAO WS-CABEC1".
       77  WS-MSG07                        PIC X(60)
                                                   VALUE
           "ERRO DE IMPRESSAO WS-CABEC2".
       77  WS-MSG08                        PIC X(60)
                                                   VALUE
           "ERRO DE IMPRESSAO WS-CABEC3".
       77  WS-MSG09                        PIC X(60)
                                                   VALUE
           "ERRO DE IMPRESSAO WS-CABEC4".
       77  WS-MSG10                        PIC X(60)
                                                   VALUE
           "ERRO DE GRAVACAO RELOCOR".
       77  WS-MSG11                        PIC X(60)
                                                   VALUE
           "ERRO DE IMPRESSAO RODAPE1".
       77  WS-MSG12                        PIC X(60)
                                                   VALUE
           "ERRO DE IMPRESSAO RODAPE2".
       77  WS-MSG13                        PIC X(60)
                                                   VALUE
           "ERRO DE FECHAMENTO MOVPRD".
       77  WS-MSG14                        PIC X(60)
                                                   VALUE
           "ERRO DE FECHAMENTO VCADPRD".
       77  WS-MSG15                        PIC X(60)
                                                   VALUE
           "ERRO DE FECHAMENTO RELOCOR".
      *--------------------------------------------------------------*
       LINKAGE                             SECTION.
      * ESTA SECTION FICARA VAZIA POIS NAO RECEBE DADOS DE OUTROS PROG
      *--------------------------------------------------------------*
       PROCEDURE                           DIVISION.
      *--------------------------------------------------------------*
      * ETAPA 3: MANIPULACAO DOS ARQUIVOS DE ENTRADA E SAIDA
       0000-GPAZ0408.
           PERFORM 1000-INICIALIZAR
           PERFORM 2000-PROCESSAR
                   UNTIL FIM-ARQ-MOVPRD
           PERFORM 3000-TERMINO
           STOP RUN
           .
       1000-INICIALIZAR.
           ACCEPT WS-HORARIO-INICIAL       FROM TIME
           MOVE 0                          TO WS-CTLIDO
                                              WS-CTINCL
                                              WS-CTEXCL
                                              WS-CTALT
                                              WS-CTINV
                                              WS-CTIMP
                                              WS-CTPAG
      *
           MOVE 99                         TO WS-CTLIN
      *
           OPEN INPUT MOVPRD
           IF NOT SUCESSO-ARQ-MOVPRD
              MOVE WS-MSG01                TO WS-MSG
              MOVE FS-MOVPRD               TO WS-FS
              GO TO 9999-ERRO
           END-IF
      *
           OPEN OUTPUT RELOCOR
           IF NOT SUCESSO-ARQ-RELOCOR
              MOVE WS-MSG02                 TO WS-MSG
              MOVE FS-RELOCOR               TO WS-FS
             GO TO 9999-ERRO
           END-IF
      *
           OPEN I-O VCADPRD
           IF NOT SUCESSO-ARQ-VCADPRD
              MOVE WS-MSG03                TO WS-MSG
              MOVE FS-VCADPRD              TO WS-FS
              GO TO 9999-ERRO
           END-IF
      *
           PERFORM 9100-FORMATA-DATA-HORA
      *
           PERFORM 1100-LER-MOVPRD
           .
       1100-LER-MOVPRD.
           READ MOVPRD INTO WS-REG-MOVPRD
           IF SUCESSO-ARQ-MOVPRD
              ADD 1                        TO WS-CTLIDO
           ELSE
              IF NOT FIM-ARQ-MOVPRD
                 MOVE WS-MSG04             TO WS-MSG
                 MOVE FS-MOVPRD            TO WS-FS
                 GO TO 9999-ERRO
              END-IF
           END-IF
           .
       2000-PROCESSAR.
           EVALUATE TRUE
              WHEN WS-TIPOMOVTO-M = "I"
                 PERFORM 2100-INCLUSAO
              WHEN WS-TIPOMOVTO-M = "E"
                 PERFORM 2200-EXCLUSAO
              WHEN WS-TIPOMOVTO-M = "A"
                 PERFORM 2300-ALTERACAO
              WHEN OTHER
                 MOVE WS-REG-MOVPRD        TO LD-IMAGEM
                 MOVE "MOVIMENTO INVALIDO" TO LD-OCORRENCIA
                 PERFORM 2400-IMPRIME-RELOCOR
                 ADD 1                     TO WS-CTINV
           END-EVALUATE
           PERFORM 1100-LER-MOVPRD
           .
       2100-INCLUSAO.
           MOVE WS-CODPROD-M               TO CODPROD-V
      *
           READ VCADPRD INTO WS-REG-VCADPRD
      *
      *    ERRO - O PRODUTO QUE SE DESEJA INCLUIR JA EXISTE
           IF SUCESSO-ARQ-VCADPRD
              MOVE WS-REG-MOVPRD           TO LD-IMAGEM
              MOVE "MOVIMENTO INVALIDO"    TO LD-OCORRENCIA
              PERFORM 2400-IMPRIME-RELOCOR
              ADD 1                        TO WS-CTINV
           ELSE
      *       OK - O PRODUTO QUE SE DESEJA INCLUIR NAO EXISTE
              IF ARQ-NAO-ENCONTRADO-VCADPRD
                 MOVE WS-REG-MOVPRD        TO WS-REG-VCADPRD
      *
                 WRITE REG-VCADPRD FROM WS-REG-VCADPRD
      *          OK - INCLUSAO OCORREU COM SUCESSO
                 IF SUCESSO-ARQ-VCADPRD
                    MOVE WS-REG-VCADPRD    TO LD-IMAGEM
                    MOVE "INCLUSAO EFETUADA"
                                           TO LD-OCORRENCIA
                    PERFORM 2400-IMPRIME-RELOCOR
                    ADD 1                  TO WS-CTINCL
                 ELSE
      *             ERRO - OUTRO PROGRAMA INCLUIU O MESMO PRODUTO
                    IF CHAVE-DUPLICADA-VCADPRD
                       MOVE WS-REG-MOVPRD  TO LD-IMAGEM
                       MOVE "MOVIMENTO INVALIDO"
                                           TO LD-OCORRENCIA
                       PERFORM 2400-IMPRIME-RELOCOR
                       ADD 1               TO WS-CTINV
      *                ERRO - FILE STATUS NAO ESPERADO
                    ELSE
                       MOVE WS-MSG05       TO WS-MSG
                       MOVE FS-VCADPRD     TO WS-FS
                       GO TO 9999-ERRO
                    END-IF
              ELSE
                 MOVE WS-MSG05             TO WS-MSG
                 MOVE FS-VCADPRD           TO WS-FS
                 GO TO 9999-ERRO
              END-IF
           END-IF
      *
           .
       2200-EXCLUSAO.
           MOVE WS-CODPROD-M               TO CODPROD-V
      *
           READ VCADPRD INTO WS-REG-VCADPRD
      *
      *    OK - P PRODUTO QUE SE DESEJA EXLUIR EXISTE
           IF SUCESSO-ARQ-VCADPRD
              DELETE VCADPRD
      *       OK - A EXCLUSAO FOI FEITA COM SUCESSO
              IF SUCESSO-ARQ-VCADPRD
                 MOVE WS-REG-VCADPRD       TO LD-IMAGEM
                 MOVE "EXCLUSAO EFETUADA"  TO LD-OCORRENCIA
                 PERFORM 2400-IMPRIME-RELOCOR
                 ADD 1                     TO WS-CTEXCL
              ELSE
      *          ERRO - OUTRO PROGRAMA EXCLUIU O MESMO PRODUTO
                 IF ARQ-NAO-ENCONTRADO-VCADPRD
                    MOVE WS-REG-MOVPRD     TO LD-IMAGEM
                    MOVE "MOVIMENTO INVALIDO"
                                           TO LD-OCORRENCIA
                    PERFORM 2400-IMPRIME-RELOCOR
                    ADD 1                  TO WS-CTINV
                 ELSE
      *             ERRO - FILE STATUS NAO ESPERADO
                    MOVE WS-MSG05          TO WS-MSG
                    MOVE FS-VCADPRD        TO WS-FS
                    GO TO 9999-ERRO
                 END-IF
              END-IF
           ELSE
      *       ERRO - O PRODUTO QUE SE DESEJA EXCLUIR NAO EXISTE
              IF ARQ-NAO-ENCONTRADO-VCADPRD
                 MOVE WS-REG-MOVPRD        TO LD-IMAGEM
                 MOVE "MOVIMENTO INVALIDO" TO LD-OCORRENCIA
                 PERFORM 2400-IMPRIME-RELOCOR
                 ADD 1                     TO WS-CTINV
              ELSE
      *          ERRO - FILE STATUS NAO ESPERADO
                 MOVE WS-MSG05             TO WS-MSG
                 MOVE FS-VCADPRD           TO WS-FS
                 GO TO 9999-ERRO
              END-IF
           END-IF
           .
       2300-ALTERACAO.
           MOVE WS-CODPROD-M               TO CODPROD-V
      *
           READ VCADPRD INTO WS-REG-VCADPRD
      *    OK - O PRODUTO QUE SE DESEJA ALTERAR EXISTE
           IF SUCESSO-ARQ-VCADPRD
              MOVE WS-REG-VCADPRD          TO LD-IMAGEM
              MOVE "ANTES ALTERACAO"       TO LD-OCORRENCIA
              PERFORM 2400-IMPRIME-RELOCOR
      *       VERIFICA SE HOUVE ALGUMA ALTERACAO
      *       IF WS-CODPROD-M NOT = SPACES
      *          MOVE WS-CODPROD-M     TO WS-CODPROD-V
      *       END-IF
              IF WS-DESCPROD-M NOT = SPACES
                 MOVE WS-DESCPROD-M    TO WS-DESCPROD-V
              END-IF
              IF WS-UNIDPROD-M NOT = SPACES
                 MOVE WS-UNIDPROD-M    TO WS-UNIDPROD-V
              END-IF
              IF WS-LOCALPROD-M NOT = SPACES
                 MOVE WS-LOCALPROD-M   TO WS-LOCALPROD-V
              END-IF
      *
              IF WS-QTDEST-M IS NUMERIC
                 MOVE WS-QTDEST-M      TO WS-QTDEST-V
              END-IF
              IF WS-QTDMAX-M IS NUMERIC
                 MOVE WS-QTDMAX-M       TO WS-QTDMAX-V
              END-IF
              IF WS-QTDMIN-M IS NUMERIC
                 MOVE WS-QTDMIN-M       TO WS-QTDMIN-V
              END-IF
              IF WS-PRECOCOMPRA-M IS NUMERIC
                 MOVE WS-PRECOCOMPRA-M TO WS-PRECOCOMPRA-V
              END-IF
              IF WS-PRECOVENDA-M IS NUMERIC
                 MOVE WS-PRECOVENDA-M  TO WS-PRECOVENDA-V
              END-IF
              IF WS-PERCOMIS-M IS NUMERIC
                 MOVE WS-PERCOMIS-M    TO WS-PERCOMIS-V
              END-IF
      *
              REWRITE REG-VCADPRD FROM WS-REG-VCADPRD
      *       OK - A ALTERACAO FOI FEITA COM SUCESSO
              IF SUCESSO-ARQ-VCADPRD
                 MOVE WS-REG-VCADPRD       TO LD-IMAGEM
                 MOVE "APOS ALTERACAO"     TO LD-OCORRENCIA
                 PERFORM 2400-IMPRIME-RELOCOR
                 ADD 1                     TO WS-CTALT
              ELSE
      *          ERRO - OUTRO PROGRAMA EXCLUIU O MESMO PRODUTO
                 IF ARQ-NAO-ENCONTRADO-VCADPRD
                    MOVE WS-REG-MOVPRD     TO LD-IMAGEM
                    MOVE "MOVIMENTO INVALIDO"
                                           TO LD-OCORRENCIA
                    PERFORM 2400-IMPRIME-RELOCOR
                    ADD 1                  TO WS-CTINV
                 ELSE
      *             ERRO - FILE STATUS NAO ESPERADO
                    MOVE WS-MSG05          TO WS-MSG
                    MOVE FS-VCADPRD        TO WS-FS
                    GO TO 9999-ERRO
                 END-IF
              END-IF
           ELSE
      *       ERRO - O PRODUTO QUE SE DESEJA ALTERAR NAO EXISTE
              IF ARQ-NAO-ENCONTRADO-VCADPRD
                 MOVE WS-REG-MOVPRD        TO LD-IMAGEM
                 MOVE "MOVIMENTO INVALIDO" TO LD-OCORRENCIA
                 PERFORM 2400-IMPRIME-RELOCOR
                 ADD 1                     TO WS-CTINV
              ELSE
      *          ERRO - FILE STATUS NAO ESPERADO
                 MOVE WS-MSG05             TO WS-MSG
                 MOVE FS-VCADPRD           TO WS-FS
                 GO TO 9999-ERRO
              END-IF
           END-IF
           .
       2400-IMPRIME-RELOCOR.
           IF WS-CTLIN > 59
              PERFORM 2500-IMPRIME-CABECALHO
           END-IF
           PERFORM 2600-IMPRIME-DETALHE
           IF WS-CTLIN = 58
              PERFORM 2700-IMPRIME-RODAPE
           END-IF
           .
       2500-IMPRIME-CABECALHO.
           MOVE WS-DATA-SYS-F              TO WS-DATA-CABEC1
      *
           MOVE WS-HORA-SYS-F              TO WS-HORA-CABEC1
      *
           ADD 1                           TO WS-CTPAG
           MOVE WS-CTPAG                   TO WS-PAG-CABEC1
      *
           WRITE REG-RELOCOR FROM WS-CABEC1 AFTER PAGE
           IF NOT SUCESSO-ARQ-RELOCOR
              MOVE WS-MSG06                TO WS-MSG
              MOVE FS-RELOCOR              TO WS-FS
              GO TO 9999-ERRO
           END-IF
      *
           WRITE REG-RELOCOR FROM WS-CABEC2
           IF NOT SUCESSO-ARQ-RELOCOR
              MOVE WS-MSG07                TO WS-MSG
              MOVE FS-RELOCOR              TO WS-FS
              GO TO 9999-ERRO
           END-IF
      *
           WRITE REG-RELOCOR FROM WS-CABEC3
           IF NOT SUCESSO-ARQ-RELOCOR
              MOVE WS-MSG08                TO WS-MSG
              MOVE FS-RELOCOR              TO WS-FS
              GO TO 9999-ERRO
           END-IF
      *
           WRITE REG-RELOCOR FROM WS-CABEC4
           IF NOT SUCESSO-ARQ-RELOCOR
              MOVE WS-MSG09                TO WS-MSG
              MOVE FS-RELOCOR              TO WS-FS
              GO TO 9999-ERRO
           END-IF
           MOVE 4                          TO WS-CTLIN
           .
       2600-IMPRIME-DETALHE.
           WRITE REG-RELOCOR FROM WS-LINDET
           IF NOT SUCESSO-ARQ-RELOCOR
              MOVE WS-MSG10                TO WS-MSG
              MOVE FS-RELOCOR              TO WS-FS
              GO TO 9999-ERRO
           END-IF
           ADD 1                           TO WS-CTLIN
           ADD 1                           TO WS-CTIMP
           .
       2700-IMPRIME-RODAPE.
           COMPUTE WS-CTPULA = 58 - WS-CTLIN
      *
           WRITE REG-RELOCOR FROM WS-RODAPE1 AFTER WS-CTPULA LINES
           IF NOT SUCESSO-ARQ-RELOCOR
              MOVE WS-MSG11                TO WS-MSG
              MOVE FS-RELOCOR              TO WS-FS
              GO TO 9999-ERRO
           END-IF
      *
           WRITE REG-RELOCOR FROM WS-RODAPE2
           IF NOT SUCESSO-ARQ-RELOCOR
              MOVE WS-MSG12                TO WS-MSG
              MOVE FS-RELOCOR              TO WS-FS
              GO TO 9999-ERRO
           END-IF
      *
           MOVE 60                         TO WS-CTLIN
           .
       3000-TERMINO.
           IF WS-CTLIN < 60
              PERFORM 2700-IMPRIME-RODAPE
           END-IF
      *
           PERFORM 9000-IMPRIME-DATA
      *
           CLOSE MOVPRD
           IF NOT SUCESSO-ARQ-MOVPRD
               MOVE WS-MSG13               TO WS-MSG
               MOVE FS-MOVPRD              TO WS-FS
               GO TO 9999-ERRO
           END-IF
      *
           CLOSE VCADPRD
           IF NOT SUCESSO-ARQ-VCADPRD
               MOVE WS-MSG14               TO WS-MSG
               MOVE FS-VCADPRD             TO WS-FS
               GO TO 9999-ERRO
           END-IF
      *
           CLOSE RELOCOR
           IF NOT SUCESSO-ARQ-MOVPRD
               MOVE WS-MSG15               TO WS-MSG
               MOVE FS-RELOCOR             TO WS-FS
               GO TO 9999-ERRO
           END-IF
      *
           ACCEPT WS-HORARIO-FINAL FROM TIME
           COPY CALCTEMP.
      *
           MOVE WS-CTLIDO                  TO WS-CTLIDO-F
           MOVE WS-CTINCL                  TO WS-CTINCL-F
           MOVE WS-CTEXCL                  TO WS-CTEXCL-F
           MOVE WS-CTALT                   TO WS-CTALT-F
           MOVE WS-CTINV                   TO WS-CTINV-F
           MOVE WS-CTLIN                   TO WS-CTLIN-F
           MOVE WS-CTPAG                   TO WS-CTPAG-F
      *
           DISPLAY "=============================================="
           DISPLAY "MOVIMENTOS LIDOS............................: "
                                                        WS-CTLIDO-F
           DISPLAY "PRODUTOS INCLUIDOS..........................: "
                                                        WS-CTINCL-F
           DISPLAY "PRODUTOS EXCLUIDOS..........................: "
                                                        WS-CTEXCL-F
           DISPLAY "PRODUTOS ALTERADOS..........................: "
                                                        WS-CTALT-F
           DISPLAY "MOVIMENTO INVALIDO..........................: "
                                                        WS-CTINV-F
           DISPLAY "LINHAS DE DETALHE IMPRESSAS.................: "
                                                        WS-CTLIN-F
           DISPLAY "PAGINAS IMPRESSAS...........................: "
                                                        WS-CTPAG-F
           DISPLAY "TEMPO TOTAL DE PROCESSAMENTO ...: "
                                            WS-TEMPO-PROCESSAMENTO-PF
           .
           COPY ROTDATA.
           COPY ROTERRO.
       9100-FORMATA-DATA-HORA.
           ACCEPT WS-DATA-SYS              FROM DATE
           MOVE CORR WS-DATA-SYS           TO WS-DATA-SYS-F
      *
           ACCEPT WS-HORA-SYS              FROM TIME
           MOVE CORR WS-HORA-SYS           TO WS-HORA-SYS-F
           .
